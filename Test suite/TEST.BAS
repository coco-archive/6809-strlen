1 GOTO5
2 P=P+1:IFPEEK(P)THEN2ELSERETURN
3 IFINKEY$=""THEN3ELSERETURN
4 C$=INKEY$:IFC$=""THEN4ELSERETURN
5 PCLEAR 1:CLEAR 512,&H5FFF:FILES 0
6 GOSUB 1000:P=0:C$=""
7 REM TIME-CRITICAL CODE
8 REM ABOVE
9 REM
10 REM        TEST.BAS
11 REM
12 REM    A PROGRAM TO TEST
13 REM       6809-STRLEN
14 REM
20 REM     BY RICHARD JOHN
21 REM      FOSTER CAVELL
22 REM  (C) 2017, 2018, 2019
23 REM
30 REM   HTTPS://GITHUB.COM/
31 REM     RICHARDCAVELL/
32 REM       6809-STRLEN
33 REM
40 REM   THIS PROGRAM IS FOR
41 REM   AN EMULATED TRS-80
42 REM    COLOR COMPUTER 2
43 REM   WITH DISK EXTENDED
44 REM       COLOR BASIC
45 REM    RUNNING IN XROAR
46 REM         OR MAME
47 REM
50 REM        IT NEEDS
51 REM    AT LEAST 32K AND
52 REM       AN EMULATED
53 REM       DISK DRIVE
54 REM
60 REM LANGUAGE SUBSCRIPTS
61 REM FOR THE SS$ ARRAY
62 E=0
63 D=1
64 F=2
65 S=3
80 REM CHANGE LN$ TO SET
81 REM THE DEFAULT LANGUAGE
82 REM E IS ENGLISH
83 REM D IS GERMAN
84 REM F IS FRENCH
85 REM S IS SPANISH
100 LN$="E"
105 REM CHANGE EA OR EB
106 REM TO SIMULATE ERRORS
107 REM 0 IS NORMAL
108 REM 1 RETURNS OFF BY 1
109 REM 2 RETURNS NO-END
110 EA=0:EB=0
120 REM OUR MACHINE LANGUAGE
121 REM WORKING SPACE
122 US=&H6000
123 DEFUSR1=US+&H100
124 DEFUSR2=US+&H180
125 PS=US+&H200
130 REM THE FILENAMES OF OUR
131 REM MACHINE LANGUAGE
132 REM PROGRAMS
133 FL=4:DIM FI$(FL-1)
134 FI$(0)="STRLEN"
135 FI$(1)="NESTRLEN"
136 FI$(2)="HELPER1"
137 FI$(3)="HELPER2"
140 REM THE SHORT STRINGS
141 REM TO BE TESTED
142 SL=6:DIM ST$(SL-1)
143 ST$(0)="HELLO, WORLD!"
144 ST$(1)="1234567890"
145 ST$(2)="<>!@$%"
146 ST$(3)="":FOR I=193 TO 220:ST$(3)=ST$(3)+CHR$(I):NEXT I
147 ST$(4)=""
148 ST$(5)="THIS IS A LONG STRING.  IT IS TOO LONG TO FIT ON ONE LINE."
150 REM THE LONG STRINGS
151 REM TO BE TESTED
152 LL=4:DIM LS(LL-1)
153 LS(0)=200
154 LS(1)=500
155 LS(2)=1024
156 LS(3)=3000
160 NL=4:DIM SS$(NL-1)
170 AR=0
171 CT=0:CI=0:CS=0
172 LA=0:L=0
173 Y=0
200 REM TITLE SCREEN
210 SS$(E)="6809-STRLEN TEST SUITE"
211 SS$(D)="6809-STRLEN TESTLAUF"
212 SS$(F)="SUITE DE TEST 6809-STRLEN"
213 SS$(S)="SECUENCIA DE PRUEBA 6809-STRLEN"
215 GOSUB 1180
220 SS$(E)="BY RICHARD CAVELL"
221 SS$(D)="VON RICHARD CAVELL"
222 SS$(F)="PAR RICHARD CAVELL"
223 SS$(S)="POR RICHARD CAVELL"
225 GOSUB 1180
230 GOSUB 1020
240 S$="HTTPS://GITHUB.COM/"
245 GOSUB 1120
250 S$="RICHARDCAVELL/6809-STRLEN"
255 GOSUB 1120
260 GOSUB 1020
270 S$="E = ENGLISH"
271 GOSUB 1040
272 S$="D = DEUTSCH"
273 GOSUB 1040
274 S$="F = FRANCAIS"
275 GOSUB 1040
276 S$="S = ESPANOL"
277 GOSUB 1040
280 GOSUB 1020
300 SS$(E)="PRESS SPACE TO START"
301 SS$(D)="PRESSE LEERTASTE ZU STARTEN"
302 SS$(F)="APPUYEZ SUR LA BARRE"
303 SS$(S)="PRESIONE LA BARRA DE ESPACIO"
304 GOSUB 1180
310 SS$(E)=""
311 SS$(D)=""
312 SS$(F)="D'ESPACE POUR DEMARRER"
313 SS$(S)="PARA EMPEZAR"
314 GOSUB 1180
320 GOSUB 1020
330 SS$(E)="(A FOR AUTORUN)"
331 SS$(D)="(A FUER AUTORUN)"
332 SS$(F)="(A FOR AUTORUN)"
333 SS$(S)="(A PARA AUTOCORRER)"
335 GOSUB 1180
340 GOSUB 4
350 IF C$="A" THEN 390
351 IF C$=" " THEN 400
352 IF C$="E" THEN 370
353 IF C$="D" THEN 370
354 IF C$="F" THEN 370
355 IF C$="S" THEN 370
360 GOTO 340
370 IF C$=LN$ THEN 340
380 LN$=C$
381 GOSUB 1000
382 GOTO 200
390 AR=1:REM FALLTHROUGH
400 REM SECOND PAGE
401 GOSUB 1000
402 GOSUB 1020
410 SS$(E)="PLEASE ENSURE THAT THE TEST"
411 SS$(D)="BITTE STELLEN SIE SICHER,"
412 SS$(F)="VEUILLEZ VOUS ASSURER QUE LE"
413 SS$(S)="POR FAVOR ASEGURESE DE QUE"
415 GOSUB 1180
420 SS$(E)="SUITE DISK IS IN THE DRIVE"
421 SS$(D)="DASS DIE TESTFOLGEN-DISK"
422 SS$(F)="DISQUE DE LA SUITE DE TEST SE"
423 SS$(S)="EL DISCO DE LA SECUENCIA DE"
425 GOSUB 1180
430 SS$(E)=""
431 SS$(D)="IM LAUFWERK IST"
432 SS$(F)="TROUVE DANS LE LECTEUR"
433 SS$(S)="PRUEBA ESTE EN EL DISPOSITIVO"
435 GOSUB 1180
437 GOSUB 1020
440 REM LOAD MACHINE
441 REM LANGUAGE ROUTINES
442 REM NAMED IN FI$()
450 FOR FX=0 TO FL-1
451 SS$(E)="   LOADING "
452 SS$(D)="   LADE "
453 SS$(F)="CHARGEMENT DE "
454 SS$(S)="CARGANDO "
455 GOSUB 1160
460 PRINT FI$(FX);
465 FOR I=LEN(FI$(FX)) TO 10
466 PRINT ".";
467 NEXT I
470 LOADM FI$(FX)
472 PRINT " ";
480 SS$(E)="DONE!"
481 SS$(D)="FERTIG!"
482 SS$(F)="FINIS"
483 SS$(S)="TERMINADO!"
484 GOSUB 1140
490 NEXT FX
495 GOSUB 1240
500 REM TEST SHORT STRINGS
501 VL=0
502 FOR SI=0 TO SL-1
503 X$=ST$(SI)
504 GOSUB 600
505 NEXT SI
510 REM TEST VERY LONG STRINGS
511 VL=1
512 FOR SI=0 TO LL-1
513 X=LS(SI)
514 GOSUB 600
515 NEXT SI
530 REM PRINT RESULTS
531 GOSUB 1000
532 SS$(E)="6809-STRLEN TEST SUITE"
533 SS$(D)="6809-STRLEN TESTLAUF"
534 SS$(F)="SUITE DE TEST 6809-STRLEN"
535 SS$(S)="SECUENCIA DE PRUEBA 6809-STRLEN"
536 GOSUB 1180
540 SS$(E)="TOTAL SCORE"
541 SS$(D)="PUNKTE INSGESAMT"
542 SS$(F)="SCORE TOTAL"
543 SS$(S)="PUNTOS TOTALES"
544 GOSUB 1180
545 GOSUB 1020
550 S$=STR$(CI)+" ("
551 S$=S$+STR$(INT((CS/CI)*100+0.5))
552 S$=S$+"% )"
555 SS$(E)=STR$(CS)+" OUT OF"+S$
556 SS$(D)=STR$(CS)+" VON"+S$
557 SS$(F)=STR$(CS)+" OUT OF"+S$
558 SS$(S)=STR$(CS)+" DE"+S$
559 GOSUB 1180
560 GOSUB 1020
570 SS$(E)="FINISHED!"
571 SS$(D)="FERTIGGESTELLT!"
572 SS$(F)="FINIS!"
573 SS$(S)="FINALIZADO!"
574 GOSUB 1140
575 END
576 REM NOTE PROGRAM ENDS HERE
600 REM TEST LENGTH OF STRING
601 GOSUB 1000
610 CT=CT+1
611 S$=MID$(STR$(CT),2)
612 SS$(E)="TEST #"+S$
613 SS$(D)="TEST #"+S$
614 SS$(F)="TEST #"+S$
615 SS$(S)="PRUEBA #"+S$
616 GOSUB 1180
620 IF VL=1 THEN GOTO 640
621 GOSUB 1020
622 SS$(E)="THE STRING IS : "
623 SS$(D)="DER STRING IST : "
624 SS$(F)="LA CHAINE EST : "
625 SS$(S)="LA CADENA DE CARACTERES ES :"
626 GOSUB 1140
630 PRINT " ";CHR$(34);X$;CHR$(34)
631 GOSUB 1020
632 GOTO 670
640 SS$(E)="VERY LONG STRING TEST"
641 SS$(D)="SEHR LANGER STRING TEST"
642 SS$(F)="TEST DE TRES LONGUE CHAINE"
643 SS$(S)="PRUEBA DE CADENA BIEN LARGA"
644 GOSUB 1180
650 SS$(E)="BE PATIENT!"
651 SS$(D)="HABEN SIE GEDULD!"
652 SS$(F)="VEUILLEZ PATIENTER!"
653 SS$(S)="SEA PACIENTE!"
654 GOSUB 1180
655 GOSUB 1020
660 SS$(E)="INTENDED LENGTH : "+STR$(X)
661 SS$(D)="VORGESEHENE LAENGE : "+STR$(X)
662 SS$(F)="LONGUEUR PREVUE : "+STR$(X)
663 SS$(S)="LENGITUD ENTENDIDA : "+STR$(X)
664 GOSUB 1140
665 GOSUB 1020
670 SS$(E)="CONSTRUCTING STRING..."
671 SS$(D)="KONSTRUIERE STRING..."
672 SS$(F)="CONSTRUCTION DE CHAINE.."
673 SS$(S)="CONSTRUYENDO CADENA..."
674 GOSUB 1160
680 IF VL=1 THEN 695
681 LA=LEN(X$)
682 IF LA=0 THEN 690
683 FOR I=0 TO (LA-1)
684 POKE (PS+I), ASC(MID$(X$,I+1,1))
685 NEXT I
690 POKE (PS+LA), 0
691 GOTO 700
695 I=USR2(X)
696 LA=X
700 SS$(E)="DONE!"
701 SS$(D)="FERTIG!"
702 SS$(F)="TERMINE"
703 SS$(S)="LISTO!"
704 GOSUB 1140
705 GOSUB 1020
710 SS$(E)="THE MEASURED LENGTH IS : "
711 SS$(D)="DIE GEMESSENE LAENGE IST : "
712 SS$(F)="LA LONGUEUR MESUREE EST : "
713 SS$(S)="LA LONGITUD MEDIDA ES : "
714 GOSUB 1140
720 IF VL=1 THEN 750
730 SS$(E)=" (BASIC AUTO)  "
731 SS$(D)=" (BASIC AUTO)  "
732 SS$(F)=" (BASIC AUTO)  "
733 SS$(S)=" (AUTO BASIC)  "
734 GOSUB 1160
740 LA=LEN(X$)
741 PRINT LA
750 SS$(E)=" (BASIC MANUAL)"
751 SS$(D)=" (BASIC MANUAL)"
752 SS$(F)=" (BASIC MANUEL)"
753 SS$(S)=" (MANUAL BASIC)"
754 GOSUB 1160
760 P=PS-1
761 GOSUB 2
762 L=P-PS
763 PRINT L;
764 GOSUB 800
770 PRINT " (STRLEN)      ";
771 GOSUB 1300
772 IF L=-1 THEN PRINT " NE "; ELSE PRINT L;
773 GOSUB 850
780 PRINT " (NESTRLEN)    ";
781 GOSUB 1320
782 IF L=-1 THEN PRINT " NE "; ELSE PRINT L;
783 GOSUB 850
784 GOSUB 1240
790 RETURN
791 REM TEST STRING
792 REM SUBROUTINE ENDS HERE
800 REM EXAMINE RESULT
810 IF L<>LA THEN 830
820 SS$(E)="(MATCH)"
821 SS$(D)="(GLEICH)"
822 SS$(F)="(MATCH)"
823 SS$(S)="(IGUAL)"
824 GOSUB 1140
825 RETURN
830 SS$(E)="(ERROR!)"
831 SS$(D)="(FEHLER!)"
832 SS$(F)="(ERREUR!)"
833 SS$(S)="(ERROR!)"
834 GOSUB 1140
835 GOSUB 1020
840 SS$(E)="THERE IS AN INTERNAL ERROR"
841 SS$(D)="ES IST EIN INTERNER FEHLER"
842 SS$(F)="IL Y UNE ERREUR INTERNE"
843 SS$(S)="HA UM ERRO INTERNO"
844 GOSUB 1140
845 END
850 IF L<>LA THEN 880
855 CS=CS+1
860 SS$(E)="(PASS)"
861 SS$(D)="(PASS)"
862 SS$(F)="(PASS)"
863 SS$(S)="(PASA)"
864 GOSUB 1140
870 RETURN
880 SS$(E)="(FAIL!)"
881 SS$(D)="(FEHL!)"
882 SS$(F)="(FAIL!)"
883 SS$(S)="(FALLA!)"
884 GOSUB 1140
885 RETURN
1000 REM CLEAR SCREEN
1001 CLS
1002 RETURN
1020 REM BLANK LINE
1021 PRINT
1022 RETURN
1040 REM PRINT S$, INDENTED
1041 PRINT "          ";S$
1042 RETURN
1100 REM CHOOSE S$ BASED
1101 REM ON LANGUAGE
1102 IF LN$="E" THEN S$=SS$(E)
1103 IF LN$="D" THEN S$=SS$(D)
1104 IF LN$="F" THEN S$=SS$(F)
1105 IF LN$="S" THEN S$=SS$(S)
1106 RETURN
1120 REM PRINT S$, CENTRE-JUSTIFIED
1121 GOSUB 1220
1122 PRINT S$
1123 RETURN
1140 REM PRINT SS$
1141 REM BASED ON LANGUAGE
1142 GOSUB 1100
1143 PRINT S$
1144 RETURN
1160 REM PRINT SS$
1161 REM BASED ON LANGUAGE
1162 REM WITH NO NEWLINE
1163 GOSUB 1100
1164 PRINT S$;
1165 RETURN
1180 REM PRINT S$
1181 REM CENTRE-JUSTIFIED
1182 REM IF NOT BLANK
1183 GOSUB 1100
1184 GOSUB 1220
1185 IF S$<>"" THEN PRINT S$
1186 RETURN
1200 REM PRINT SS$
1201 REM CENTRE-JUSTIFIED
1202 REM IF NOT BLANK
1203 REM NO NEWLINE
1204 GOSUB 1100
1205 GOSUB 1220
1206 IF S$<>"" THEN PRINT S$;
1207 RETURN
1220 I=(32-LEN(S$))/2:IF I>=1 THEN PRINT STRING$(INT(I),32);
1221 RETURN
1240 REM IF AUTORUN IS NOT
1241 REM ON, ASK FOR
1242 REM KEYPRESS, THEN
1243 REM WAIT FOR IT
1244 IF AR=1 THEN RETURN
1245 PRINT
1250 SS$(E)="PRESS ANY KEY TO CONTINUE"
1251 SS$(D)="DRUECKEN SIE EINE BELIEBIGE"
1252 SS$(F)="APPUYEZ SUR N'IMPORTE QUELLE"
1253 SS$(S)="PRESIONE CUALQUIER TECLA"
1254 GOSUB 1180
1260 SS$(E)=""
1261 SS$(D)="TASTE UM FORTZUFAHREN"
1262 SS$(F)="TOUCHE POUR CONTINUER"
1263 SS$(S)="PARA CONTINUAR"
1264 GOSUB 1200
1265 GOTO 3
1300 REM USE STRLEN
1301 Y=0+EA*256
1302 GOTO 1340
1320 REM USE NESTRLEN
1321 Y=1+EB*256
1340 L=USR1(Y)
1341 CI=CI+1
1342 IF L<-1 THEN L=L+32768
1343 REM -1 IS NO-END
1344 REM ANY OTHER NEGATIVE
1345 REM RESULT SHOULD BE
1346 REM CORRECTED
1347 RETURN
