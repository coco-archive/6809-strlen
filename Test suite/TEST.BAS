1 GOTO5
2 P=P+1:IFPEEK(P)THEN2ELSERETURN
3 IFINKEY$=""THEN3ELSERETURN
4 C$=INKEY$:IFC$=""THEN4ELSERETURN
5 PCLEAR 1:CLEAR 512,&H5FFF:FILES 0
6 GOSUB 20000:P=0:C$=""
7 REM TIME-CRITICAL CODE
8 REM ABOVE
9 REM
10 REM        TEST.BAS
11 REM
12 REM    A PROGRAM TO TEST
13 REM       6809-STRLEN
14 REM
20 REM     BY RICHARD JOHN
21 REM      FOSTER CAVELL
22 REM  (C) 2017, 2018, 2019
23 REM
30 REM   HTTPS://GITHUB.COM/
31 REM     RICHARDCAVELL/
32 REM       6809-STRLEN
33 REM
40 REM   THIS PROGRAM IS FOR
41 REM   AN EMULATED TRS-80
42 REM    COLOR COMPUTER 2
43 REM   WITH DISK EXTENDED
44 REM       COLOR BASIC
45 REM    RUNNING IN XROAR
46 REM         OR MAME
47 REM
50 REM        IT NEEDS
51 REM    AT LEAST 32K AND
52 REM       AN EMULATED
53 REM       DISK DRIVE
54 REM
60 REM LANGUAGE SUBSCRIPTS
61 REM FOR THE SS$ ARRAY
62 E=0
63 D=1
64 F=2
65 S=3
80 REM CHANGE LN$ TO SET
81 REM THE DEFAULT LANGUAGE
82 REM E IS ENGLISH
83 REM D IS GERMAN
84 REM F IS FRENCH
85 REM S IS SPANISH
100 LN$="E"
105 REM CHANGE EA OR EB
106 REM TO SIMULATE ERRORS
107 REM 0 IS NORMAL
108 REM 1 RETURNS OFF BY 1
109 REM 2 RETURNS NO-END
110 EA=0:EB=0
120 REM OUR MACHINE LANGUAGE
121 REM WORKING SPACE
122 US=&H6000
123 DEFUSR1=US+&H100
124 DEFUSR2=US+&H180
125 PS=US+&H200
130 REM THE FILENAMES OF OUR
131 REM MACHINE LANGUAGE
132 REM PROGRAMS
133 FL=4:DIM FI$(FL-1)
134 FI$(0)="STRLEN"
135 FI$(1)="NESTRLEN"
136 FI$(2)="HELPER1"
137 FI$(3)="HELPER2"
140 REM THE SHORT STRINGS
141 REM TO BE TESTED
142 SL=6:DIM ST$(SL-1)
143 ST$(0)="HELLO, WORLD!"
144 ST$(1)="1234567890"
145 ST$(2)="<>!@$%"
146 ST$(3)="":FOR I=193 TO 220:ST$(3)=ST$(3)+CHR$(I):NEXT I
147 ST$(4)=""
148 ST$(5)="THIS IS A LONG STRING.  IT IS TOO LONG TO FIT ON ONE LINE."
150 REM THE LONG STRINGS
151 REM TO BE TESTED
152 LL=4:DIM LS(LL-1)
153 LS(0)=200
154 LS(1)=500
155 LS(2)=1024
156 LS(3)=3000
160 NL=4:DIM SS$(NL-1)
170 X$="" : X=0
171 AR=0
172 CT=0 : CI=0 : CS=0
173 LA=0 : L=0
174 J=0
175 Y=0
176 Z=0
200 REM TITLE SCREEN
210 SS$(E)="6809-STRLEN TEST SUITE"
211 SS$(D)="6809-STRLEN TESTLAUF"
212 SS$(F)="SUITE DE TEST 6809-STRLEN"
213 SS$(S)="SECUENCIA DE PRUEBA 6809-STRLEN"
215 GOSUB 30000
220 SS$(E)="BY RICHARD CAVELL"
221 SS$(D)="VON RICHARD CAVELL"
222 SS$(F)="PAR RICHARD CAVELL"
223 SS$(S)="POR RICHARD CAVELL"
225 GOSUB 30000
230 GOSUB 20500
240 S$="HTTPS://GITHUB.COM/"
245 GOSUB 26000
250 S$="RICHARDCAVELL/6809-STRLEN"
255 GOSUB 26000
260 GOSUB 20500
270 S$="E = ENGLISH"
271 GOSUB 22000
272 S$="D = DEUTSCH"
273 GOSUB 22000
274 S$="F = FRANCAIS"
275 GOSUB 22000
276 S$="S = ESPANOL"
277 GOSUB 22000
280 GOSUB 20500
300 SS$(E)="PRESS SPACE TO START"
301 SS$(D)="PRESSE LEERTASTE ZU STARTEN"
302 SS$(F)="APPUYEZ SUR LA BARRE"
303 SS$(S)="PRESIONE LA BARRA DE ESPACIO"
304 GOSUB 30000
310 SS$(E)=""
311 SS$(D)=""
312 SS$(F)="D'ESPACE POUR DEMARRER"
313 SS$(S)="PARA EMPEZAR"
314 GOSUB 30000
320 GOSUB 20500
330 SS$(E)="(A FOR AUTORUN)"
331 SS$(D)="(A FUER AUTORUN)"
332 SS$(F)="(A FOR AUTORUN)"
333 SS$(S)="(A PARA AUTOCORRER)"
335 GOSUB 30000
340 GOSUB 4
3500 IF C$="A" THEN 3900
3510 IF C$=" " THEN 4000
3520 IF C$="E" THEN 3800
3530 IF C$="D" THEN 3800
3540 IF C$="F" THEN 3800
3550 IF C$="S" THEN 3800
3700 GOTO 3400
3800 IF C$=LN$ THEN 3400
3810 LN$=C$
3820 GOSUB 20000 : GOTO 200
3900 AR=1 : REM FALLTHROUGH
4000 REM SECOND PAGE
4010 GOSUB 20000 : PRINT
4100 SS$(E)="PLEASE ENSURE THAT THE TEST"
4110 SS$(D)="BITTE STELLEN SIE SICHER,"
4120 SS$(F)="VEUILLEZ VOUS ASSURER QUE LE"
4130 SS$(S)="POR FAVOR ASEGURESE DE QUE"
4150 GOSUB 30000
4200 SS$(E)="SUITE DISK IS IN THE DRIVE"
4210 SS$(D)="DASS DIE TESTFOLGEN-DISK"
4220 SS$(F)="DISQUE DE LA SUITE DE TEST SE"
4230 SS$(S)="EL DISCO DE LA SECUENCIA DE"
4250 GOSUB 30000
4300 SS$(E)=""
4310 SS$(D)="IM LAUFWERK IST"
4320 SS$(F)="TROUVE DANS LE LECTEUR"
4330 SS$(S)="PRUEBA ESTE EN EL DISPOSITIVO"
4350 GOSUB 30000 : PRINT
4400 GOSUB 40000
5000 REM TEST STRINGS
5010 Z=0
5020 FOR J = 0 TO SL-1
5030 X$=ST$(J)
5040 GOSUB 10000
5050 NEXT J
5200 REM TEST VERY LONG STRINGS
5210 Z=1
5220 FOR J = 0 TO LL-1
5230 X=LS(J) : GOSUB 10000
5240 NEXT J
5500 REM PRINT RESULTS
5550 GOSUB 20000
5600 SS$(E)="6809-STRLEN TEST SUITE"
5610 SS$(D)="6809-STRLEN TESTLAUF"
5620 SS$(F)="SUITE DE TEST 6809-STRLEN"
5630 SS$(S)="SECUENCIA DE PRUEBA 6809-STRLEN"
5650 GOSUB 30000
5700 SS$(E)="TOTAL SCORE"
5710 SS$(D)="PUNKTE INSGESAMT"
5720 SS$(F)="SCORE TOTAL"
5730 SS$(S)="PUNTOS TOTALES"
5750 GOSUB 30000 : PRINT
5900 S$=STR$(CI)+" ("
5910 S$=S$+STR$(INT((CS/CI)*100+0.5))
5920 S$=S$+"% )"
6000 SS$(E)=STR$(CS)+" OUT OF"+S$
6010 SS$(D)=STR$(CS)+" VON"+S$
6020 SS$(F)=STR$(CS)+" OUT OF"+S$
6030 SS$(S)=STR$(CS)+" DE"+S$
6050 GOSUB 30000 : PRINT
6200 SS$(E)="FINISHED!"
6210 SS$(D)="FERTIGGESTELLT!"
6220 SS$(F)="FINIS!"
6230 SS$(S)="FINALIZADO!"
6250 GOSUB 28000
6300 END
10000 REM TEST LENGTH OF STRING
10100 GOSUB 20000
10110 GOSUB 42000
10120 IF Z=1 THEN GOTO 10400
10200 PRINT
10210 SS$(E)="THE STRING IS : "
10220 SS$(D)="DER STRING IST : "
10230 SS$(F)="LA CHAINE EST : "
10240 SS$(S)="LA CADENA DE CARACTERES ES :"
10260 GOSUB 28000
10300 PRINT " ";CHR$(34);X$;CHR$(34)
10320 PRINT
10330 GOTO 10800
10400 SS$(E)="VERY LONG STRING TEST"
10410 SS$(D)="SEHR LANGER STRING TEST"
10420 SS$(F)="TEST DE TRES LONGUE CHAINE"
10430 SS$(S)="PRUEBA DE CADENA BIEN LARGA"
10450 GOSUB 30000
10500 SS$(E)="BE PATIENT!"
10510 SS$(D)="HABEN SIE GEDULD!"
10520 SS$(F)="VEUILLEZ PATIENTER!"
10530 SS$(S)="SEA PACIENTE!"
10540 GOSUB 30000 : PRINT
10700 SS$(E)="INTENDED LENGTH : "+STR$(X)
10710 SS$(D)="VORGESEHENE LAENGE : "+STR$(X)
10720 SS$(F)="LONGUEUR PREVUE : "+STR$(X)
10730 SS$(S)="LENGITUD ENTENDIDA : "+STR$(X)
10750 GOSUB 28000 : PRINT
10800 SS$(E)="CONSTRUCTING STRING..."
10810 SS$(D)="KONSTRUIERE STRING..."
10820 SS$(F)="CONSTRUCTION DE CHAINE.."
10830 SS$(S)="CONSTRUYENDO CADENA..."
10850 GOSUB 29000
10880 IF Z=1 THEN 11000
10900 LA=LEN(X$)
10910 IF LA<1 THEN 10950
10920 FOR I=0 TO (LA-1)
10930 POKE (PS+I), ASC(MID$(X$,I+1,1))
10940 NEXT I
10950 POKE (PS+LA), 0
10960 GOTO 11100
11000 I=USR2(X)
11010 LA=X
11100 SS$(E)="DONE!"
11110 SS$(D)="FERTIG!"
11120 SS$(F)="TERMINE"
11130 SS$(S)="LISTO!"
11150 GOSUB 28000 : PRINT
12000 SS$(E)="THE MEASURED LENGTH IS : "
12010 SS$(D)="DIE GEMESSENE LAENGE IST : "
12020 SS$(F)="LA LONGUEUR MESUREE EST : "
12030 SS$(S)="LA LONGITUD MEDIDA ES : "
12050 GOSUB 28000
12080 IF Z=1 THEN 12200
12100 SS$(E)=" (BASIC AUTO)  "
12110 SS$(D)=" (BASIC AUTO)  "
12120 SS$(F)=" (BASIC AUTO)  "
12130 SS$(S)=" (AUTO BASIC)  "
12150 GOSUB 29000
12180 LA=LEN(X$)
12190 PRINT LA
12200 SS$(E)=" (BASIC MANUAL)"
12210 SS$(D)=" (BASIC MANUAL)"
12220 SS$(F)=" (BASIC MANUEL)"
12230 SS$(S)=" (MANUAL BASIC)"
12250 GOSUB 29000
12260 GOSUB 44000
12280 PRINT L;
12290 GOSUB 18000
12300 PRINT " (STRLEN)      ";
12310 GOSUB 45000
12320 IF L=-1 THEN PRINT " NE "; ELSE PRINT L;
12330 GOSUB 19000
12400 PRINT " (NESTRLEN)    ";
12410 GOSUB 46000
12420 IF L=-1 THEN PRINT " NE "; ELSE PRINT L; 
12430 GOSUB 19000
12500 GOSUB 41000
13000 RETURN
18000 IF L<>LA THEN 18500
18100 SS$(E)="(MATCH)"
18110 SS$(D)="(GLEICH)"
18120 SS$(F)="(MATCH)"
18130 SS$(S)="(IGUAL)"
18150 GOSUB 28000
18200 RETURN
18500 SS$(E)="(ERROR!)"
18510 SS$(D)="(FEHLER!)"
18520 SS$(F)="(ERREUR!)"
18530 SS$(S)="(ERROR!)"
18550 GOSUB 28000 : PRINT
18600 SS$(E)="THERE IS AN INTERNAL ERROR"
18610 SS$(D)="ES IST EIN INTERNER FEHLER"
18620 SS$(F)="IL Y UNE ERREUR INTERNE"
18630 SS$(S)="HA UM ERRO INTERNO"
18650 GOSUB 28000
18700 END
19000 IF L<>LA THEN 19500
19100 CS=CS+1
19200 SS$(E)="(PASS)"
19210 SS$(D)="(PASS)"
19220 SS$(F)="(PASS)"
19230 SS$(S)="(PASA)"
19250 GOSUB 28000 : RETURN
19500 SS$(E)="(FAIL!)"
19510 SS$(D)="(FEHL!)"
19520 SS$(F)="(FAIL!)"
19530 SS$(S)="(FALLA!)"
19550 GOSUB 28000 : RETURN
20000 REM CLEAR SCREEN
20100 CLS
20200 RETURN
20500 REM BLANK LINE
20600 PRINT
20700 RETURN
22000 REM PRINT S$, INDENTED
22100 PRINT "          ";S$
22200 RETURN
23000 REM PRINT S$, INDENTED
23010 REM  WITH NO NEWLINE
23100 PRINT "          ";S$;
23200 RETURN
25000 REM CHOOSE S$ BASED
25010 REM ON LANGUAGE
25100 IF LN$="E" THEN S$=SS$(E)
25110 IF LN$="D" THEN S$=SS$(D)
25120 IF LN$="F" THEN S$=SS$(F)
25130 IF LN$="S" THEN S$=SS$(S)
25200 RETURN
26000 REM PRINT S$, CENTRE-JUSTIFIED
26100 GOSUB 32000
26200 PRINT S$
26300 RETURN
27000 REM PRINT S$, CENTRE-JUSTIFIED
27010 REM WITH NO NEWLINE
27100 GOSUB 32000
27200 PRINT S$;
27300 RETURN
28000 REM PRINT SS$
28010 REM BASED ON LANGUAGE
28100 GOSUB 25000
28200 PRINT S$
28300 RETURN
29000 REM PRINT SS$
29010 REM BASED ON LANGUAGE
29020 REM WITH NO NEWLINE
29100 GOSUB 25000
29200 PRINT S$;
29300 RETURN
30000 REM PRINT S$
30010 REM CENTRE-JUSTIFIED
30020 REM IF NOT BLANK
30100 GOSUB 25000
30150 GOSUB 32000
30200 IF S$<>"" THEN PRINT S$
30300 RETURN
31000 REM PRINT SS$
31010 REM CENTRE-JUSTIFIED
31020 REM IF NOT BLANK
31030 REM NO NEWLINE
31100 GOSUB 25000
31150 GOSUB 32000
31200 IF S$<>"" THEN PRINT S$;
31300 RETURN
32000 I=(32-LEN(S$))/2:IFI>=1THENPRINTSTRING$(INT(I),32);
32100 RETURN
40000 REM LOAD MACHINE
40010 REM LANGUAGE ROUTINES
40020 REM NAMED IN FI$()
40050 FOR FX = 0 TO FL-1
40100 SS$(E)="   LOADING "
40110 SS$(D)="   LADE "
40120 SS$(F)="CHARGEMENT DE "
40130 SS$(S)="CARGANDO "
40150 GOSUB 29000
40200 PRINT FI$(FX);
40210 FOR I=LEN(FI$(FX)) TO 10
40220 PRINT ".";
40230 NEXT I
40300 LOADM FI$(FX)
40400 SS$(E)=" DONE!"
40410 SS$(D)=" FERTIG!"
40420 SS$(F)=" FINIS"
40430 SS$(S)=" TERMINADO!"
40450 GOSUB 28000
40460 NEXT FX
40470 RETURN
41000 REM IF AUTORUN IS NOT
41010 REM ON, ASK FOR
41020 REM KEYPRESS, THEN
41030 REM WAIT FOR IT
41100 IF AR=1 THEN RETURN
41150 PRINT
41200 SS$(E)="PRESS ANY KEY TO CONTINUE"
41210 SS$(D)="DRUECKEN SIE EINE BELIEBIGE"
41220 SS$(F)="APPUYEZ SUR N'IMPORTE QUELLE"
41230 SS$(S)="PRESIONE CUALQUIER TECLA"
41250 GOSUB 30000
41300 SS$(E)=""
41310 SS$(D)="TASTE UM FORTZUFAHREN"
41320 SS$(F)="TOUCHE POUR CONTINUER"
41330 SS$(S)="PARA CONTINUAR"
41350 GOSUB 31000
41400 GOTO 3
42000 REM COUNT THE TESTS
42100 CT=CT+1
42150 S$=STR$(CT):S$=MID$(S$,2)
42200 SS$(E)="TEST #"+S$
42210 SS$(D)="TEST #"+S$
42220 SS$(F)="TEST #"+S$
42230 SS$(S)="PRUEBA #"+S$
42250 GOSUB 30000 : RETURN
44000 REM MEASURE STRING
44010 REM LENGTH USING BASIC
44100 P=PS-1 : GOSUB 2 : L=P-PS
44200 RETURN
45000 REM USE STRLEN
45100 Y=0+EA*256
45200 GOTO 47000
46000 REM USE NESTRLEN
46100 Y=1+EB*256
47000 L=USR1(Y)
47010 CI=CI+1
47020 IF L < -1 THEN L = L + 32768
47030 REM -1 IS NO-END
47040 REM ANY OTHER NEGATIVE
47050 REM RESULT SHOULD BE
47060 REM CORRECTED
47100 RETURN
