1 GOTO5
2 I=I+1:IFPEEK(I)THEN2ELSERETURN
3 IFINKEY$=""THEN3ELSERETURN
4 C$=INKEY$:IFC$=""THEN4ELSERETURN
5 PCLEAR1:CLEAR512,&H5FFF:CLS
6 I=0:C$=""
7 REM TIME-CRITICAL CODE
8 REM ABOVE
100 REM
110 REM     TEST.BAS
120 REM
130 REM A PROGRAM TO TEST
140 REM    6809-STRLEN
150 REM
200 REM BY RICHARD JOHN
210 REM  FOSTER CAVELL
220 REM (C) 2017, 2018, 2019
230 REM
300 REM HTTPS://GITHUB.COM/
310 REM   RICHARDCAVELL/
320 REM    6809-STRLEN
330 REM
400 REM THIS PROGRAM IS FOR
410 REM AN EMULATED TRS-80
420 REM  COLOR COMPUTER 2
430 REM WITH DISK EXTENDED
440 REM    COLOR BASIC
450 REM  RUNNING IN XROAR
460 REM      OR MAME
470 REM
500 REM     IT NEEDS
510 REM AT LEAST 32K AND
520 REM   AN EMULATED
530 REM    DISK DRIVE
600 REM
620 REM LANGUAGE SUBSCRIPTS
630 REM FOR THE S$ ARRAY
650 E = 0
660 D = 1
670 F = 2
680 S = 3
700 REM CHANGE LN$ TO SET
710 REM THE DEFAULT LANGUAGE
720 REM E IS ENGLISH
730 REM D IS GERMAN
740 REM F IS FRENCH
750 REM S IS SPANISH
800 LN$="E"
850 REM CHANGE EA OR EB
860 REM TO SIMULATE ERRORS
870 REM 0 IS NORMAL
880 REM 1 RETURNS OFF BY 1
890 REM 2 RETURNS NO-END
900 EA=0 : EB=0
1000 REM OUR MACHINE LANGUAGE
1010 REM WORKING SPACE
1100 US=&H6000
1110 DEFUSR1=US+&H100
1120 DEFUSR2=US+&H180
1130 PS=US+&H200
1200 REM THE FILENAMES OF OUR
1210 REM MACHINE LANGUAGE
1220 REM PROGRAMS
1230 FL=4:DIM FI$(FL)
1240 FI$(0)="STRLEN"
1250 FI$(1)="NESTRLEN"
1260 FI$(2)="HELPER1"
1270 FI$(3)="HELPER2"
1300 REM THE SHORT STRINGS
1310 REM TO BE TESTED
1320 SL=6:DIM ST$(SL)
1330 ST$(0)="HELLO, WORLD!"
1340 ST$(1)="1234567890"
1350 ST$(2)="<>!@$%"
1360 ST$(3)="":FORI=193TO220:ST$(3)=ST$(3)+CHR$(I):NEXTI
1370 ST$(4)=""
1380 ST$(5)="THIS IS A LONG STRING.  IT IS TOO LONG TO FIT ON ONE LINE."
1400 REM THE LONG STRINGS
1410 REM TO BE TESTED
1500 LL=4:DIM LS(LL)
1520 LS(0)=200
1530 LS(1)=500
1540 LS(2)=1024
1550 LS(3)=3000
1600 X$="" : X=0
1700 NL=4:DIM S$(NL)
1710 SP$=""
1720 S$(0)=""
1730 S$(1)=""
1740 S$(2)=""
1750 S$(3)=""
1800 AR=0
1820 CT=0 : CI=0 : CS=0
1900 LA=0 : L=0
1910 J=0
1950 Y=0
1960 Z=0
2000 REM TITLE SCREEN
2100 S$(E)="6809-STRLEN TEST SUITE"
2110 S$(D)="6809-STRLEN TESTLAUF"
2120 S$(F)="SUITE DE TEST 6809-STRLEN"
2130 S$(S)="SECUENCIA DE PRUEBA 6809-STRLEN"
2150 GOSUB 30000
2200 S$(E)="BY RICHARD CAVELL"
2210 S$(D)="VON RICHARD CAVELL"
2220 S$(F)="PAR RICHARD CAVELL"
2230 S$(S)="POR RICHARD CAVELL"
2250 GOSUB 30000 : PRINT
2400 SP$="HTTPS://GITHUB.COM/"
2410 GOSUB 26000
2500 SP$="RICHARDCAVELL/6809-STRLEN"
2510 GOSUB 26000 : PRINT
2600 SP$="E = ENGLISH"
2610 GOSUB 22000
2620 SP$="D = DEUTSCH"
2630 GOSUB 22000
2640 SP$="F = FRANCAIS"
2650 GOSUB 22000
2660 SP$="S = ESPANOL"
2670 GOSUB 22000 : PRINT
3000 S$(E)="PRESS SPACE TO START"
3010 S$(D)="PRESSE LEERTASTE ZU STARTEN"
3020 S$(F)="APPUYEZ SUR LA BARRE"
3030 S$(S)="PRESIONE LA BARRA DE ESPACIO"
3040 GOSUB 30000
3100 S$(E)=""
3110 S$(D)=""
3120 S$(F)="D'ESPACE POUR DEMARRER"
3130 S$(S)="PARA EMPEZAR"
3140 GOSUB 30000 : PRINT
3300 S$(E)="(A FOR AUTORUN)"
3310 S$(D)="(A FUER AUTORUN)"
3320 S$(F)="(A FOR AUTORUN)"
3330 S$(S)="(A PARA AUTOCORRER)"
3350 GOSUB 30000
3400 GOSUB 4
3500 IF C$="A" THEN 3900
3510 IF C$=" " THEN 4000
3520 IF C$="E" THEN 3800
3530 IF C$="D" THEN 3800
3540 IF C$="F" THEN 3800
3550 IF C$="S" THEN 3800
3700 GOTO 3400
3800 IF C$=LN$ THEN 3400
3810 LN$=C$
3820 CLS : GOTO 2000
3900 AR=1 : REM FALLTHROUGH
4000 REM SECOND PAGE
4010 CLS : PRINT
4100 S$(E)="PLEASE ENSURE THAT THE TEST"
4110 S$(D)="BITTE STELLEN SIE SICHER,"
4120 S$(F)="VEUILLEZ VOUS ASSURER QUE LE"
4130 S$(S)="POR FAVOR ASEGURESE DE QUE"
4150 GOSUB 30000
4200 S$(E)="SUITE DISK IS IN THE DRIVE"
4210 S$(D)="DASS DIE TESTFOLGEN-DISK"
4220 S$(F)="DISQUE DE LA SUITE DE TEST SE"
4230 S$(S)="EL DISCO DE LA SECUENCIA DE"
4250 GOSUB 30000
4300 S$(E)=""
4310 S$(D)="IM LAUFWERK IST"
4320 S$(F)="TROUVE DANS LE LECTEUR"
4330 S$(S)="PRUEBA ESTE EN EL DISPOSITIVO"
4350 GOSUB 30000 : PRINT
4400 GOSUB 40000
5000 REM TEST STRINGS
5010 Z=0
5020 FOR J = 0 TO SL-1
5030 X$=ST$(J)
5040 GOSUB 10000
5050 NEXT J
5200 REM TEST VERY LONG STRINGS
5210 Z=1
5220 FOR J = 0 TO LL-1
5230 X=LS(J) : GOSUB 10000
5240 NEXT J
5500 REM PRINT RESULTS
5550 CLS
5600 S$(E)="6809-STRLEN TEST SUITE"
5610 S$(D)="6809-STRLEN TESTLAUF"
5620 S$(F)="SUITE DE TEST 6809-STRLEN"
5630 S$(S)="SECUENCIA DE PRUEBA 6809-STRLEN"
5650 GOSUB 30000
5700 S$(E)="TOTAL SCORE"
5710 S$(D)="PUNKTE INSGESAMT"
5720 S$(F)="SCORE TOTAL"
5730 S$(S)="PUNTOS TOTALES"
5750 GOSUB 30000 : PRINT
5900 SP$=STR$(CI)+" ("
5910 SP$=SP$+STR$(INT((CS/CI)*100+0.5))
5920 SP$=SP$+"% )"
6000 S$(E)=STR$(CS)+" OUT OF"+SP$
6010 S$(D)=STR$(CS)+" VON"+SP$
6020 S$(F)=STR$(CS)+" OUT OF"+SP$
6030 S$(S)=STR$(CS)+" DE"+SP$
6050 GOSUB 30000 : PRINT
6200 S$(E)="FINISHED!"
6210 S$(D)="FERTIGGESTELLT!"
6220 S$(F)="FINIS!"
6230 S$(S)="FINALIZADO!"
6250 GOSUB 28000
6300 END
10000 REM TEST LENGTH OF STRING
10100 CLS
10110 GOSUB 42000
10120 IF Z=1 THEN GOTO 10400
10200 PRINT
10210 S$(E)="THE STRING IS : "
10220 S$(D)="DER STRING IST : "
10230 S$(F)="LA CHAINE EST : "
10240 S$(S)="LA CADENA DE CARACTERES ES :"
10260 GOSUB 28000
10300 PRINT " ";CHR$(34);X$;CHR$(34)
10320 PRINT
10330 GOTO 10800
10400 S$(E)="VERY LONG STRING TEST"
10410 S$(D)="SEHR LANGER STRING TEST"
10420 S$(F)="TEST DE TRES LONGUE CHAINE"
10430 S$(S)="PRUEBA DE CADENA BIEN LARGA"
10450 GOSUB 30000
10500 S$(E)="BE PATIENT!"
10510 S$(D)="HABEN SIE GEDULD!"
10520 S$(F)="VEUILLEZ PATIENTER!"
10530 S$(S)="SEA PACIENTE!"
10540 GOSUB 30000 : PRINT
10700 S$(E)="INTENDED LENGTH : "+STR$(X)
10710 S$(D)="VORGESEHENE LAENGE : "+STR$(X)
10720 S$(F)="LONGUEUR PREVUE : "+STR$(X)
10730 S$(S)="LENGITUD ENTENDIDA : "+STR$(X)
10750 GOSUB 28000 : PRINT
10800 S$(E)="CONSTRUCTING STRING..."
10810 S$(D)="KONSTRUIERE STRING..."
10820 S$(F)="CONSTRUCTION DE CHAINE.."
10830 S$(S)="CONSTRUYENDO CADENA..."
10850 GOSUB 29000
10880 IF Z=1 THEN 11000
10900 LA=LEN(X$)
10910 IF LA<1 THEN 10950
10920 FOR I=0 TO (LA-1)
10930 POKE (PS+I), ASC(MID$(X$,I+1,1))
10940 NEXT I
10950 POKE (PS+LA), 0
10960 GOTO 11100
11000 I=USR2(X)
11010 LA=X
11100 S$(E)="DONE!"
11110 S$(D)="FERTIG!"
11120 S$(F)="TERMINE"
11130 S$(S)="LISTO!"
11150 GOSUB 28000 : PRINT
12000 S$(E)="THE MEASURED LENGTH IS : "
12010 S$(D)="DIE GEMESSENE LAENGE IST : "
12020 S$(F)="LA LONGUEUR MESUREE EST : "
12030 S$(S)="LA LONGITUD MEDIDA ES : "
12050 GOSUB 28000
12080 IF Z=1 THEN 12200
12100 S$(E)=" (BASIC AUTO)  "
12110 S$(D)=" (BASIC AUTO)  "
12120 S$(F)=" (BASIC AUTO)  "
12130 S$(S)=" (AUTO BASIC)  "
12150 GOSUB 29000
12180 LA=LEN(X$)
12190 PRINT LA
12200 S$(E)=" (BASIC MANUAL)"
12210 S$(D)=" (BASIC MANUAL)"
12220 S$(F)=" (BASIC MANUEL)"
12230 S$(S)=" (MANUAL BASIC)"
12250 GOSUB 29000
12260 GOSUB 44000
12280 PRINT L;
12290 GOSUB 18000
12300 PRINT " (STRLEN)      ";
12310 GOSUB 45000
12320 IF L=-1 THEN PRINT " NE "; ELSE PRINT L;
12330 GOSUB 19000
12400 PRINT " (NESTRLEN)    ";
12410 GOSUB 46000
12420 IF L=-1 THEN PRINT " NE "; ELSE PRINT L; 
12430 GOSUB 19000
12500 GOSUB 41000
13000 RETURN
18000 IF L<>LA THEN 18500
18100 S$(E)="(MATCH)"
18110 S$(D)="(GLEICH)"
18120 S$(F)="(MATCH)"
18130 S$(S)="(IGUAL)"
18150 GOSUB 28000
18200 RETURN
18500 S$(E)="(ERROR!)"
18510 S$(D)="(FEHLER!)"
18520 S$(F)="(ERREUR!)"
18530 S$(S)="(ERROR!)"
18550 GOSUB 28000 : PRINT
18600 S$(E)="THERE IS AN INTERNAL ERROR"
18610 S$(D)="ES IST EIN INTERNER FEHLER"
18620 S$(F)="IL Y UNE ERREUR INTERNE"
18630 S$(S)="HA UM ERRO INTERNO"
18650 GOSUB 28000
18700 END
19000 IF L<>LA THEN 19500
19100 CS=CS+1
19200 S$(E)="(PASS)"
19210 S$(D)="(PASS)"
19220 S$(F)="(PASS)"
19230 S$(S)="(PASA)"
19250 GOSUB 28000 : RETURN
19500 S$(E)="(FAIL!)"
19510 S$(D)="(FEHL!)"
19520 S$(F)="(FAIL!)"
19530 S$(S)="(FALLA!)"
19550 GOSUB 28000 : RETURN
22000 REM PRINT SP$, INDENTED
22100 PRINT "          ";SP$
22200 RETURN
23000 REM PRINT SP$, INDENTED
23010 REM  WITH NO NEWLINE
23100 PRINT "          ";SP$;
23200 RETURN
25000 REM CHOOSE SP$ BASED
25010 REM ON LANGUAGE
25100 IF LN$="E" THEN SP$=S$(E)
25110 IF LN$="D" THEN SP$=S$(D)
25120 IF LN$="F" THEN SP$=S$(F)
25130 IF LN$="S" THEN SP$=S$(S)
25200 RETURN
26000 REM PRINT SP$, CENTRE-JUSTIFIED
26100 GOSUB 32000
26200 PRINT SP$
26300 RETURN
27000 REM PRINT SP$, CENTRE-JUSTIFIED
27010 REM WITH NO NEWLINE
27100 GOSUB 32000
27200 PRINT SP$;
27300 RETURN
28000 REM PRINT S$
28010 REM BASED ON LANGUAGE
28100 GOSUB 25000
28200 PRINT SP$
28300 RETURN
29000 REM PRINT S$
29010 REM BASED ON LANGUAGE
29020 REM WITH NO NEWLINE
29100 GOSUB 25000
29200 PRINT SP$;
29300 RETURN
30000 REM PRINT SP$
30010 REM CENTRE-JUSTIFIED
30020 REM IF NOT BLANK
30100 GOSUB 25000
30150 GOSUB 32000
30200 IF SP$<>"" THEN PRINT SP$
30300 RETURN
31000 REM PRINT S$
31010 REM CENTRE-JUSTIFIED
31020 REM IF NOT BLANK
31030 REM NO NEWLINE
31100 GOSUB 25000
31150 GOSUB 32000
31200 IF SP$<>"" THEN PRINT SP$;
31300 RETURN
32000 I=(32-LEN(SP$))/2:IFI>=1THENPRINTSTRING$(INT(I),32);
32100 RETURN
40000 REM LOAD MACHINE
40010 REM LANGUAGE ROUTINES
40020 REM NAMED IN FI$()
40050 FOR FX = 0 TO FL-1
40100 S$(E)="   LOADING "
40110 S$(D)="   LADE "
40120 S$(F)="CHARGEMENT DE "
40130 S$(S)="CARGANDO "
40150 GOSUB 29000
40200 PRINT FI$(FX);
40210 FOR I=LEN(FI$(FX)) TO 10
40220 PRINT ".";
40230 NEXT I
40300 LOADM FI$(FX)
40400 S$(E)=" DONE!"
40410 S$(D)=" FERTIG!"
40420 S$(F)=" FINIS"
40430 S$(S)=" TERMINADO!"
40450 GOSUB 28000
40460 NEXT FX
40470 RETURN
41000 REM IF AUTORUN IS NOT
41010 REM ON, ASK FOR
41020 REM KEYPRESS, THEN
41030 REM WAIT FOR IT
41100 IF AR=1 THEN RETURN
41150 PRINT
41200 S$(E)="PRESS ANY KEY TO CONTINUE"
41210 S$(D)="DRUECKEN SIE EINE BELIEBIGE"
41220 S$(F)="APPUYEZ SUR N'IMPORTE QUELLE"
41230 S$(S)="PRESIONE CUALQUIER TECLA"
41250 GOSUB 30000
41300 S$(E)=""
41310 S$(D)="TASTE UM FORTZUFAHREN"
41320 S$(F)="TOUCHE POUR CONTINUER"
41330 S$(S)="PARA CONTINUAR"
41350 GOSUB 31000
41400 GOTO 3
42000 REM COUNT THE TESTS
42100 CT=CT+1
42150 SP$=STR$(CT):SP$=MID$(SP$,2)
42200 S$(E)="TEST #"+SP$
42210 S$(D)="TEST #"+SP$
42220 S$(F)="TEST #"+SP$
42230 S$(S)="PRUEBA #"+SP$
42250 GOSUB 30000 : RETURN
44000 REM MEASURE STRING
44010 REM LENGTH USING BASIC
44100 I=PS-1 : GOSUB 2 : L=I-PS
44200 RETURN
45000 REM USE STRLEN
45100 Y=0+EA*256
45200 GOTO 47000
46000 REM USE NESTRLEN
46100 Y=1+EB*256
47000 L=USR1(Y)
47010 CI=CI+1
47020 IF L < -1 THEN L = L + 32768
47030 REM -1 IS NO-END
47040 REM ANY OTHER NEGATIVE
47050 REM RESULT SHOULD BE
47060 REM CORRECTED
47100 RETURN
