1 GOTO6
2 I=I+1:IFPEEK(I)THEN2ELSERETURN
3 IFINKEY$=""THEN3ELSERETURN
4 S$=INKEY$:IFS$=""THEN4ELSERETURN
5 PCLEAR1:CLEAR512,&H5FFF:CLS
6 I=0:S$=""
7 REM CRITICAL CODE ABOVE
100 REM     TEST.BAS
110 REM
120 REM A PROGRAM TO TEST
130 REM    6809-STRLEN
140 REM
200 REM BY RICHARD JOHN
210 REM  FOSTER CAVELL
220 REM (C) 2017, 2018
230 REM
300 REM HTTPS://GITHUB.COM/
310 REM   RICHARDCAVELL/
320 REM    6809-STRLEN
330 REM
400 REM THIS PROGRAM IS FOR
410 REM AN EMULATED TRS-80
420 REM  COLOR COMPUTER 2
430 REM WITH DISK EXTENDED
440 REM    COLOR BASIC
450 REM  RUNNING IN XROAR
460 REM      OR MAME
470 REM
500 REM     IT NEEDS
510 REM AT LEAST 32K AND
520 REM   AN EMULATED
530 REM    DISK DRIVE
800 LN$="E"
820 REM CHANGE LN$ TO SET
830 REM THE DEFAULT LANGUAGE
840 REM E IS ENGLISH
850 REM D IS GERMAN
860 REM F IS FRENCH
870 REM S IS SPANISH
900 EA=0 : EB=0
920 REM CHANGE EA OR EB
930 REM TO SIMULATE ERRORS
940 REM 0 IS NORMAL
950 REM 1 IS OFF BY 1
960 REM 2 IS NO-END
1000 US=&H6000
1110 DEFUSR1=US+&H100
1120 DEFUSR2=US+&H180
1130 PS=US+&H200
1200 FA$="STRLEN"
1210 FB$="NESTRLEN"
1220 FC$="HELPER1"
1230 FD$="HELPER2"
1240 FI$=""
1300 A$="HELLO, WORLD!"
1310 B$="1234567890"
1320 C$="<>!@$%"
1330 D$="" : E$=""
1400 FORI=193TO220:E$=E$+CHR$(I):NEXTI
1450 F$="THIS IS A LONG STRING.  IT IS TOO LONG TO FIT ON ONE LINE."
1500 A=1024 : B=3000
1600 X$="" : X=0
1700 S$=""
1720 SE$="" : SD$="" : SF$="" : SS$=""
1800 AR=0
1820 CT=0 : CI=0 : CS=0
1900 LA=0 : L=0
1950 Y=0
1960 Z=0
2000 REM TITLE SCREEN
2100 SE$="6809-STRLEN TEST SUITE"
2110 SD$="6809-STRLEN TESTLAUF"
2120 SF$="SUITE DE TEST 6809-STRLEN"
2130 SS$="SECUENCIA DE PRUEBA 6809-STRLEN"
2150 GOSUB 30000
2200 SE$="BY RICHARD CAVELL"
2210 SD$="VON RICHARD CAVELL"
2220 SF$="PAR RICHARD CAVELL"
2230 SS$="POR RICHARD CAVELL"
2250 GOSUB 30000 : PRINT
2400 S$="HTTPS://GITHUB.COM/"
2410 GOSUB 26000
2500 S$="RICHARDCAVELL/6809-STRLEN"
2510 GOSUB 26000 : PRINT
2600 S$="E = ENGLISH"
2610 GOSUB 22000
2620 S$="D = DEUTSCH"
2630 GOSUB 22000
2640 S$="F = FRANCAIS"
2650 GOSUB 22000
2660 S$="S = ESPANOL"
2670 GOSUB 22000 : PRINT
3000 SE$="PRESS SPACE TO START"
3010 SD$="PRESSE LEERTASTE ZU STARTEN"
3020 SF$="APPUYEZ SUR LA BARRE"
3030 SS$="PRESIONE LA BARRA DE ESPACIO"
3040 GOSUB 30000
3100 SE$=""
3110 SD$=""
3120 SF$="D'ESPACE POUR DEMARRER"
3130 SS$="PARA EMPEZAR"
3140 GOSUB 30000 : PRINT
3300 SE$="(A FOR AUTORUN)"
3310 SD$="(A FUER AUTORUN)"
3320 SF$="(A FOR AUTORUN)"
3330 SS$="(A PARA AUTOCORRER)"
3350 GOSUB 30000 : GOSUB 4
3500 IF S$="A" THEN 3900
3510 IF S$=" " THEN 4000
3520 IF S$="E" THEN 3800
3530 IF S$="D" THEN 3800
3540 IF S$="F" THEN 3800
3550 IF S$="S" THEN 3800
3700 GOTO 3500
3800 IF S$=LN$ THEN 3500
3810 LN$=S$
3820 CLS : GOTO 2000
3900 AR=1 : REM FALLTHROUGH
4000 REM SECOND PAGE
4010 CLS : PRINT
4100 SE$="PLEASE ENSURE THAT THE TEST"
4110 SD$="BITTE STELLEN SIE SICHER,"
4120 SF$="VEUILLEZ VOUS ASSURER QUE LE"
4130 SS$="POR FAVOR ASEGURESE DE QUE"
4150 GOSUB 30000
4200 SE$="SUITE DISK IS IN THE DRIVE"
4210 SD$="DASS DIE TESTFOLGEN-DISK"
4220 SF$="DISQUE DE LA SUITE DE TEST SE"
4230 SS$="EL DISCO DE LA SECUENCIA DE"
4250 GOSUB 30000
4300 SE$=""
4310 SD$="IM LAUFWERK IST"
4320 SF$="TROUVE DANS LE LECTEUR"
4330 SS$="PRUEBA ESTE EN EL DISPOSITIVO"
4350 GOSUB 30000 : PRINT
4600 FI$=FA$ : GOSUB 40000
4610 FI$=FB$ : GOSUB 40000
4620 FI$=FC$ : GOSUB 40000
4630 FI$=FD$ : GOSUB 40000
5000 REM TEST STRINGS
5010 Z=0
5020 X$=A$ : GOSUB 10000
5030 X$=B$ : GOSUB 10000
5040 X$=C$ : GOSUB 10000
5050 X$=D$ : GOSUB 10000
5060 X$=E$ : GOSUB 10000
5070 X$=F$ : GOSUB 10000
5200 REM TEST VERY LONG STRINGS
5210 Z=1
5220 X=A : GOSUB 10000
5230 X=B : GOSUB 10000
5500 REM PRINT RESULTS
5550 CLS
5600 SE$="6809-STRLEN TEST SUITE"
5610 SD$="6809-STRLEN TESTLAUF"
5620 SF$="SUITE DE TEST 6809-STRLEN"
5630 SS$="SECUENCIA DE PRUEBA 6809-STRLEN"
5650 GOSUB 30000
5700 SE$="TOTAL SCORE"
5710 SD$="PUNKTE INSGESAMT"
5720 SF$="SCORE TOTAL"
5730 SS$="PUNTOS TOTALES"
5750 GOSUB 30000 : PRINT
5900 S$=STR$(CI)+" ("
5910 S$=S$+STR$(INT((CS/CI)*100+0.5))
5920 S$=S$+"% )"
6000 SE$=STR$(CS)+" OUT OF"+S$
6010 SD$=STR$(CS)+" VON"+S$
6020 SF$=STR$(CS)+" OUT OF"+S$
6030 SS$=STR$(CS)+" DE"+S$
6050 GOSUB 30000 : PRINT
6200 SE$="FINISHED!"
6210 SD$="FERTIGGESTELLT!"
6220 SF$="FINIS!"
6230 SS$="FINALIZADO!"
6250 GOSUB 28000
6300 END
10000 REM TEST LENGTH OF STRING
10100 CLS
10110 GOSUB 42000
10120 IF Z=1 THEN GOTO 10400
10200 PRINT
10210 SE$="THE STRING IS : "
10220 SD$="DER STRING IST : "
10230 SF$="LA CHAINE EST : "
10240 SS$="LA CADENA DE CARACTERES ES :"
10260 GOSUB 28000
10300 PRINT " ";CHR$(34);X$;CHR$(34)
10320 PRINT
10330 GOTO 10800
10400 SE$="VERY LONG STRING TEST"
10410 SD$="SEHR LANGER STRING TEST"
10420 SF$="TEST DE TRES LONGUE CHAINE"
10430 SS$="PRUEBA DE CADENA BIEN LARGA"
10450 GOSUB 30000
10500 SE$="BE PATIENT!"
10510 SD$="HABEN SIE GEDULD!"
10520 SF$="VEUILLEZ PATIENTER!"
10530 SS$="SEA PACIENTE!"
10540 GOSUB 30000 : PRINT
10700 SE$="INTENDED LENGTH : "+STR$(X)
10710 SD$="VORGESEHENE LAENGE : "+STR$(X)
10720 SF$="LONGUEUR PREVUE : "+STR$(X)
10730 SS$="LENGITUD ENTENDIDA : "+STR$(X)
10750 GOSUB 28000 : PRINT
10800 SE$="CONSTRUCTING STRING..."
10810 SD$="KONSTRUIERE STRING..."
10820 SF$="CONSTRUCTION DE CHAINE.."
10830 SS$="CONSTRUYENDO CADENA..."
10850 GOSUB 29000
10880 IF Z=1 THEN 11000
10900 LA=LEN(X$)
10910 IF LA<1 THEN 10950
10920 FOR I=0 TO (LA-1)
10930 POKE (PS+I), ASC(MID$(X$,I+1,1))
10940 NEXT I
10950 POKE (PS+LA), 0
10960 GOTO 11100
11000 I=USR2(X)
11010 LA=X
11100 SE$="DONE!"
11110 SD$="FERTIG!"
11120 SF$="TERMINE"
11130 SS$="LISTO!"
11150 GOSUB 28000 : PRINT
12000 SE$="THE MEASURED LENGTH IS : "
12010 SD$="DIE GEMESSENE LAENGE IST : "
12020 SF$="LA LONGUEUR MESUREE EST : "
12030 SS$="LA LONGITUD MEDIDA ES : "
12050 GOSUB 28000
12080 IF Z=1 THEN 12200
12100 SE$=" (BASIC AUTO)  "
12110 SD$=" (BASIC AUTO)  "
12120 SF$=" (BASIC AUTO)  "
12130 SS$=" (AUTO BASIC)  "
12150 GOSUB 29000
12180 LA=LEN(X$)
12190 PRINT LA
12200 SE$=" (BASIC MANUAL)"
12210 SD$=" (BASIC MANUAL)"
12220 SF$=" (BASIC MANUEL)"
12230 SS$=" (MANUAL BASIC)"
12250 GOSUB 29000
12260 GOSUB 44000
12280 PRINT L;
12290 GOSUB 18000
12300 PRINT " (STRLEN)      ";
12310 GOSUB 45000
12320 IF L=-1 THEN PRINT " NE "; ELSE PRINT L;
12330 GOSUB 19000
12400 PRINT " (NESTRLEN)    ";
12410 GOSUB 46000
12420 IF L=-1 THEN PRINT " NE "; ELSE PRINT L; 
12430 GOSUB 19000
12500 GOSUB 41000
13000 RETURN
18000 IF L<>LA THEN 18500
18100 SE$="(MATCH)"
18110 SD$="(GLEICH)"
18120 SF$="(MATCH)"
18130 SS$="(IGUAL)"
18150 GOSUB 28000
18200 RETURN
18500 SE$="(ERROR!)"
18510 SD$="(FEHLER!)"
18520 SF$="(ERREUR!)"
18530 SS$="(ERROR!)"
18550 GOSUB 28000 : PRINT
18600 SE$="THERE IS AN INTERNAL ERROR"
18610 SD$="ES IST EIN INTERNER FEHLER"
18620 SF$="IL Y UNE ERREUR INTERNE"
18630 SS$="HA UM ERRO INTERNO"
18650 GOSUB 28000
18700 END
19000 IF L<>LA THEN 19500
19100 CS=CS+1
19200 SE$="(PASS)"
19210 SD$="(PASS)"
19220 SF$="(PASS)"
19230 SS$="(PASA)"
19250 GOSUB 28000 : RETURN
19500 SE$="(FAIL!)"
19510 SD$="(FEHL!)"
19520 SF$="(FAIL!)"
19530 SS$="(FALLA!)"
19550 GOSUB 28000 : RETURN
22000 REM PRINT S$, INDENTED
22100 PRINT "          ";S$
22200 RETURN
23000 REM PRINT S$, INDENTED
23010 REM  WITH NO NEWLINE
23100 PRINT "          ";S$;
23200 RETURN
25000 REM CHOOSE S$ BASED
25010 REM ON LANGUAGE
25100 IF LN$="E" THEN S$=SE$
25110 IF LN$="D" THEN S$=SD$
25120 IF LN$="F" THEN S$=SF$
25130 IF LN$="S" THEN S$=SS$
25200 RETURN
26000 REM PRINT S$, CENTRE-JUSTIFIED
26100 GOSUB 32000
26200 PRINT S$
26300 RETURN
27000 REM PRINT S$, CENTRE-JUSTIFIED
27010 REM WITH NO NEWLINE
27100 GOSUB 32000
27200 PRINT S$;
27300 RETURN
28000 REM PRINT SE$/SD$/ETC
28010 REM BASED ON LANGUAGE
28100 GOSUB 25000
28200 PRINT S$
28300 RETURN
29000 REM PRINT SE$/SD$/ETC
29010 REM BASED ON LANGUAGE
29020 REM WITH NO NEWLINE
29100 GOSUB 25000
29200 PRINT S$;
29300 RETURN
30000 REM PRINT SE$/SD$/ETC
30010 REM CENTRE-JUSTIFIED
30020 REM IF NOT BLANK
30100 GOSUB 25000
30150 GOSUB 32000
30200 IF S$<>"" THEN PRINT S$
30300 RETURN
31000 REM PRINT SE$/SD$/ETC
31010 REM CENTRE-JUSTIFIED
31020 REM IF NOT BLANK
31030 REM NO NEWLINE
31100 GOSUB 25000
31150 GOSUB 32000
31200 IF S$<>"" THEN PRINT S$;
31300 RETURN
32000 I=(32-LEN(S$))/2:IFI>=1THENPRINTSTRING$(INT(I),32);
32100 RETURN
40000 REM LOAD MACHINE
40010 REM LANGUAGE ROUTINE
40020 REM NAMED IN FI$
40100 SE$="   LOADING "
40110 SD$="   LADE "
40120 SF$="CHARGEMENT DE "
40130 SS$="CARGANDO "
40150 GOSUB 29000
40200 PRINT FI$;
40210 FOR I=LEN(FI$) TO 10
40220 PRINT ".";
40230 NEXT I
40300 LOADM FI$
40400 SE$=" DONE!"
40410 SD$=" FERTIG!"
40420 SF$=" FINIS"
40430 SS$=" TERMINADO!"
40450 GOSUB 28000 : RETURN
41000 REM IF AUTORUN IS NOT
41010 REM ON, ASK FOR
41020 REM KEYPRESS, THEN
41030 REM WAIT FOR IT
41100 IF AR=1 THEN RETURN
41150 PRINT
41200 SE$="PRESS ANY KEY TO CONTINUE"
41210 SD$="DRUECKEN SIE EINE BELIEBIGE"
41220 SF$="APPUYEZ SUR N'IMPORTE QUELLE"
41230 SS$="PRESIONE CUALQUIER TECLA"
41250 GOSUB 30000
41300 SE$=""
41310 SD$="TASTE UM FORTZUFAHREN"
41320 SF$="TOUCHE POUR CONTINUER"
41330 SS$="PARA CONTINUAR"
41350 GOSUB 31000
41400 GOTO 3
42000 REM COUNT THE TESTS
42100 CT=CT+1
42150 S$=STR$(CT)
42200 SE$="TEST #"+S$
42210 SD$=SE$
42220 SF$=SE$
42230 SS$="PRUEBA #"+S$
42250 GOSUB 30000 : RETURN
44000 REM MEASURE STRING
44010 REM LENGTH USING BASIC
44100 I=PS-1 : GOSUB 2 : L=I-PS
44200 RETURN
45000 REM USE STRLEN
45100 Y=0+EA*256
45200 GOTO 47000
46000 REM USE NESTRLEN
46100 Y=1+EB*256
47000 L=USR1(Y)
47010 CI=CI+1
47100 RETURN
