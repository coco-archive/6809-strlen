10 REM STRLEN TEST PROGRAM
20 REM  BY RICHARD JOHN
30 REM   FOSTER CAVELL
40 REM     (C) 2017
50 REM
100 REM LOAD STRLEN.BIN INTO
110 REM   THE MEMORY LOCATION
120 REM   GIVEN AT LINE 320
130 REM LOAD STRLEN-TEST.BIN
140 REM   INTO THE MEMORY
150 REM  LOCATION GIVEN AT
160 REM  LINE 330
170 REM THEN JUST RUN THIS
180 REM  PROGRAM.
200 PCLEAR 1
210 CLEAR 1, &H2F00
290 REM
300 LET PA = &H2FFC
310 LET PB = &H2FFE
320 LET PM = &H3000
330 LET PN = &H3100
340 LET PS = &H3200
390 REM
500 REM EACH OF THESE STRINGS
510 REM  WILL BE MEASURED
520 REM
530 A$ = "HELLO, WORLD!"
540 B$ = "1234567890"
550 C$ = "<>!@$%"
560 D$ = "THIS IS A REALLY LONG STRING.  I AM NOT KIDDING.  IT IS REALLY LONG."
570 E$ = ""
690 REM
700 REM A VERY LONG STRING
710 REM WILL BE CREATED
720 REM WITH THIS LENGTH
730 REM
740 LET Z = 1024
790 REM
800 CLS
810 PRINT "      STRLEN.S TEST SUITE"
820 PRINT "       BY RICHARD CAVELL"
830 PRINT "           (C) 2017"
840 PRINT
850 PRINT "      HTTPS://GITHUB.COM/"
860 PRINT "   RICHARDCAVELL/6809-STRLEN"
870 PRINT
900 PRINT "      PRESS A KEY TO START"
990 GOSUB 5000
1000 REM NOW MEASURE
1010 REM EACH STRING
1020 REM
1120 X$ = A$ : GOSUB 2000
1130 X$ = B$ : GOSUB 2000
1140 X$ = C$ : GOSUB 2000
1150 X$ = D$ : GOSUB 2000
1160 X$ = E$ : GOSUB 2000
1190 REM
1300 REM NOW DO THAT
1310 REM LONG STRING
1500 CLS
1600 PRINT "     VERY LONG";
1610 PRINT " STRING TEST"
1620 PRINT
1630 PRINT "         (BE";
1640 PRINT " PATIENT!)"
1650 GOSUB 6000
1660 GOSUB 3000
1700 PRINT
1710 PRINT " CONSTRUCTED";
1720 PRINT " LENGTH : "; Z
1730 PRINT " CALCULATED";
1740 PRINT " LENGTH  : "; L
1750 PRINT
1790 REM END OF PROGRAM
1800 IF Z = L GOTO 1850
1820 PRINT "TEST FAILED!"
1830 GOTO 1900
1850 PRINT "TEST PASSED!"
1900 PRINT "ALL FINISHED"
1910 END
1990 REM MAIN PROGRAM FINISHED
2000 REM SUBROUTINE TO MEASURE
2010 REM  STRING LENGTH
2020 REM  INPUT IS IN X$
2030 REM
2040 CLS
2050 PRINT "THE STRING IS : "
2060 PRINT " ";CHR$(34);
2070 PRINT X$;CHR$(34)
2080 PRINT
2090 REM
2100 IF LEN(X$) < 1 GOTO 2190
2110 FOR I = 0 TO (LEN(X$) - 1)
2120 POKE (PS + I),                  ASC(MID$(X$,I + 1,1))
2130 NEXT I
2190 REM
2200 POKE (PS + LEN(X$)), 0
2210 REM
2310 GOSUB 3000
2400 PRINT "THE CALCULATED";
2410 PRINT " LENGTH IS : "
2420 PRINT " (BASIC) "; LEN(X$)
2430 PRINT " (ASM)   "; L
2440 PRINT
2500 IF LEN(X$) = L GOTO 2550
2510 PRINT "TEST FAILED!"
2520 GOTO 2600
2550 PRINT "TEST PASSED!"
2600 PRINT
2610 PRINT "PRESS ANY KEY TO CONTINUE"
2620 GOSUB 5000
2700 RETURN
3000 REM CALL STRLEN
3010 REM
3020 REM RESULT IS IN LENGTH
3100 POKE PA, INT(PM / 256)
3110 LET I = PM : GOSUB 4000
3120 POKE (PA + 1), I
3130 POKE PB,                        INT(PS / 256)
3140 LET I = PS : GOSUB 4000
3150 POKE (PB + 1), I
3200 EXEC PN
3210 LET L = PEEK(PB)                 * 256 + PEEK(PB + 1)
3220 RETURN
3990 REM
4000 REM THIS ROUTINE DOES
4010 REM I = I MOD 256
4020 REM
4100 REM VALUE PASSED IN I
4110 IF I < 256 THEN GOTO 4130
4120 I = I - 256 : GOTO 4110
4130 RETURN
5000 REM WAIT FOR KEYPRESS
5010 IF INKEY$ = "" GOTO 5010
5020 RETURN
6000 REM CREATE STRING OF
6010 REM I CHARS AT PS
6020 FOR I = (PS) TO                 (PS + Z - 1)
6030 POKE I, ASC("A")
6040 NEXT I
6050 POKE (PS + Z), 0
6060 RETURN
